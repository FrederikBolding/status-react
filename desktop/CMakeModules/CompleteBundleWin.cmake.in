# windeployqt should be used here, but since we get the `Not implemented` error from it, we're trying to manually copy artifacts to output directory
set(TARGET_DIR $<TARGET_FILE_DIR:${APP_NAME}>)

set(qtmodules Core Gui Widgets Qml Quick QuickControls2 QuickTemplates2 WebSockets OpenGL Svg Network Concurrent Positioning SerialPort)
if(USE_QTWEBKIT)
  set(qtmodules ${qtmodules} Multimedia WebKit WebKitWidgets WebChannel)

  add_custom_command(TARGET @APP_NAME@ POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_if_different
          "@CONAN_BIN_DIRS_QT5-MXE@/QtWebProcess.exe"
          ${TARGET_DIR})
endif()
foreach(qtmodule ${qtmodules})
  message("Copying ${qtmodule} module")
  add_custom_command(TARGET @APP_NAME@ POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_if_different
          "@CONAN_BIN_DIRS_QT5-MXE@/Qt5${qtmodule}.dll"
          ${TARGET_DIR})
endforeach(qtmodule ${qtmodules})

set(qtplugindirs "platforms" "styles" "iconengines" "imageformats" "translations")
foreach(qtplugindir ${qtplugindirs})
  message("Copying plugin ${qtplugindir}")
  add_custom_command(TARGET @APP_NAME@ POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
        "@CONAN_BIN_DIRS_QT5-MXE@/../plugins/${qtplugindir}/"
        "${TARGET_DIR}/${qtplugindir}")
endforeach()

set(qtqmldirs "QtQuick" "QtQuick.2" "QtGraphicalEffects" "QtPositioning" "QtWebSockets")
if(USE_QTWEBKIT)
  set(qtqmldirs ${qtqmldirs} "QtWebKit" "QtWebChannel")
endif()
foreach(qtqmldir ${qtqmldirs})
  message("Copying QML dir for ${qtqmldir}")
  add_custom_command(TARGET @APP_NAME@ POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
        "@CONAN_BIN_DIRS_QT5-MXE@/../qml/${qtqmldir}/"
        "${TARGET_DIR}/${qtqmldir}")
endforeach()

set(deps_qt5 "libpng16-16" "libharfbuzz-0" "zlib1" "libpcre2-16-0"
             "libpcre-1" "libcrypto-1_1-x64" "libssl-1_1-x64" "libfreetype-6"
             "libglib-2.0-0" "libstdc++-6" "libbz2" "libintl-8" "libiconv-2"
             "icuin56" "icuuc56" "icudt56" "libjpeg-9" "libsqlite3-0" "libwebp-5" "libgcc_s_seh-1")
foreach(lib ${deps_qt5})
  set(lib_full_path "@CONAN_BIN_DIRS_MXETOOLCHAIN-X86_64-W64-MINGW32@/${lib}.dll") 
  if(NOT EXISTS "${lib_full_path}")
    set(lib_full_path "@CONAN_BIN_DIRS_QT5-MXE@/${lib}.dll") 
  endif()
  add_custom_command(TARGET @APP_NAME@ POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_if_different
      ${lib_full_path}
      ${TARGET_DIR})
endforeach(lib ${deps_qt5})
